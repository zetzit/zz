using <string.h> as c_string;
using <stdio.h> as stdio;
using <stdarg.h>::{va_list, va_start, va_end};
using <stdio.h>::{vsnprintf, printf, FILE};

export struct String+ {
    usize   len;
    char    mem[];
}

export fn clear(String+tail mut * self) {
    c_string::memset(self->mem, 0, tail);
    self->len = 0;
}

export fn push(String+t mut * self, char cstr) -> bool
{
    if self->len > t - 2 {
        return false;
    }

    static_attest(self->len < len(self->mem));
    (self->mem)[self->len] = cstr;
    self->len += 1;
    return true;
}

export fn pop(String+t mut * self) -> bool {
    if self->len == 0 {
        return false;
    }
    self->len -= 1;
    static_attest(self->len < len(self->mem));
    (self->mem)[self->len] = 0;
    return true;
}

export fn append_cstr(String+t mut * self, char * cstr)
    where nullterm(cstr)
    model nullterm(self->mem)
{
    usize mut inlen = strlen(cstr);

    if inlen + self->len > t - 1 {
        inlen = t - 1 - self->len;
    }

    c_string::memcpy(self->mem + self->len, cstr, inlen);
    self->len += inlen;

    static_attest(nullterm(self->mem))
}

export fn append(String+t mut * self, String+t2 * other)
{
    usize mut inlen = other->len;

    if inlen + self->len > t - 1 {
        inlen = t - 1 - self->len;
    }

    c_string::memcpy(self->mem + self->len, other->mem, inlen);
    self->len += inlen;

    static_attest(nullterm(self->mem))
}


export fn append_bytes(String+t mut* self, u8* bytes, usize mut inlen)
    where len(bytes) >= inlen
{
    if inlen + self->len > t - 1 {
        inlen = t - 1 - self->len;
    }

    c_string::memcpy(self->mem + self->len, bytes, inlen);
    self->len += inlen;
}

export fn format(String+tail mut * self, char *fmt, ...) -> int
    where tail > 0
{
    va_list mut args;
    va_start (args, fmt);

    usize isfree = tail - self->len;

    int r = as<int>(vsnprintf(
        self->mem +  self->len,
        isfree,
        fmt,
        args
    ));
    va_end (args);

    if r > 0 {
        if (usize)r >= isfree {
            self->len += (isfree - 1);
        } else {
            self->len += as<usize>(r);
        }
    }

    static_attest(self->len < len(self->mem));
    (self->mem)[self->len] = 0;

    return r;
}

export fn eq_cstr(String+tail* self, char * unsafe b) -> bool
    where nullterm(b)
{
    return as<int>(c_string::strcmp(self->mem,b)) == 0;
}

export fn cstr_eq(char *unsafe a, char * unsafe b) -> bool
    where a == 0 || nullterm(a)
    where b == 0 || nullterm(b)
{
    return as<int>(c_string::strcmp(a,b)) == 0;
}

export fn starts_with_cstr(String+tail* self, char *unsafe a) -> bool {
    usize alen = as<usize>(c_string::strlen(a));
    if alen > self->len {
        return false;
    }
    return as<int>(c_string::strncmp(self->mem,a,alen)) == 0;
}

export fn fgets(String+tail mut* self, FILE mut * unsafe stream) -> bool {
    char *rr = (char*)stdio::fgets(self->mem + self->len, tail - self->len, stream);
    if rr == 0 {
        return false;
    }

    char * extmem = self->mem + self->len;
    static_attest(safe(extmem));
    static_attest(nullterm(extmem));
    self->len += strlen(extmem);
    return true;
}



export fn substr(String+tail *self, usize from, usize mut size, String+tail2 mut* other)
{
    static_attest(len(other->mem) >= tail2);
    static_attest(len(self->mem) >= tail);

    if self->len == 0 {
        return;
    }

    if from >= self->len {
        return;
    }

    if tail2 == 0 {
        return;
    }

    if tail == 0 {
        return;
    }

    if size == 0 {
        size = self->len;
    }

    if from + size >= self->len {
        size = self->len - from;
    }

    //printf("A len1: %zu, len2: %zu, from: %zu, size: %zu\n",
    //    self->len, other->len, from, size);

    if other->len + size + 1 >= tail2 {
        if other->len + 1 >= tail2 {
            return;
        }
        size = tail2 - 1 - other->len;
    }

    //TODO i'm not sure what ssa is confused about
    static_attest(size + other->len < tail2);

    //printf("B len1: %zu, len2: %zu, from: %zu, size: %zu\n",
    //    self->len, other->len, from, size);


    if size == 0 {
        return;
    }

    c_string::memcpy(other->mem + other->len, self->mem + from, size);
    (other->mem)[size + other->len] = 0;
    other->len += size;
}





export fn split(String+tail *self, char token, usize mut *iterator, String+tail2 mut* other) -> bool
{
    static_attest(len(self->mem) >= self->len);

    usize start = *iterator;

    if *iterator >= self->len {
        return false;
    }

    for (; *iterator < self->len; (*iterator)++) {
        if (self->mem)[*iterator] == token {
            break;
        }
    }

    usize size = *iterator - start;
    *iterator += 1;
    if size == 0 {
        return true;
    }

    substr(self, start, size, other);
    return true;
}


export fn space(String+tail *self) -> usize {
    return tail - self->len;
}

export fn strlen(char *s) -> usize
    where nullterm(s)
    model return == len(s)
{
    usize l = (usize)c_string::strlen(s);
    static_attest(l == len(s));
    return l;
}
